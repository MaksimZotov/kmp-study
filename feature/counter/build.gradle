import org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask

plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.compose)
    alias(libs.plugins.kotlin.multiplatform)
    alias(libs.plugins.kotlin.plugin.compose)
    alias(libs.plugins.ksp)
}

kotlin {
    androidTarget {
        compilations.all {
            kotlinOptions {
                jvmTarget = "17"
            }
        }
    }

    [iosX64(), iosArm64(), iosSimulatorArm64()].each { target ->
        target.binaries.framework {
            baseName = "FeatureCounter"
            isStatic = true
        }
    }

    sourceSets {
        commonMain.dependencies {
            implementation project(':core:resources')
            implementation project(':core:routing')
            implementation project(':core:ui')

            implementation project(':client:counter')

            implementation libs.androidx.ui
            implementation libs.androidx.ui.graphics
            implementation libs.androidx.material3
            implementation libs.androidx.resources

            implementation libs.koin.core
            implementation libs.koin.compose
            implementation libs.koin.compose.viewmodel
            implementation libs.koin.annotations
            implementation libs.androidx.lifecycle.viewmodel
            implementation libs.androidx.navigation.compose
        }
        androidMain.dependencies {
            implementation libs.androidx.ui.tooling
            implementation libs.androidx.ui.tooling.preview
        }
    }

    // KSP Common sourceSet
    sourceSets.named("commonMain").configure {
        kotlin.srcDir("build/generated/ksp/metadata/commonMain/kotlin")
    }
}

// KSP Tasks
dependencies {
    add("kspCommonMainMetadata", libs.koin.ksp.compiler)
    add("kspAndroid", libs.koin.ksp.compiler)
    add("kspIosX64", libs.koin.ksp.compiler)
    add("kspIosArm64", libs.koin.ksp.compiler)
    add("kspIosSimulatorArm64", libs.koin.ksp.compiler)
}

// KSP Metadata Trigger
tasks.withType(KotlinCompilationTask).configureEach {
    if (name != "kspCommonMainKotlinMetadata") {
        dependsOn "kspCommonMainKotlinMetadata"
    }
}

ksp {
    arg("KOIN_USE_COMPOSE_VIEWMODEL","true")
    arg("KOIN_CONFIG_CHECK","true")
}

android {
    namespace 'com.mz.kmpstudy.feature.counter'
    compileSdk 35

    defaultConfig {
        minSdk 24
        targetSdk 35

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}